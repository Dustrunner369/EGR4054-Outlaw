class Player {
    field int x;
    field int y;
    field int width;
    field int height;
    field int prev_x;
    field int prev_y;
    field int direction; // 1 = up, 2 = down, 3 = left, 4 = right
    field boolean alive;

    static int BOUNDARY_BUFFER;
    
    constructor Player new(int x_in, int y_in) {
        let BOUNDARY_BUFFER = 2;
        let x = x_in;
        let y = y_in;
        let width = 20;
        let height = 20;
        let prev_x = x;
        let prev_y = y;
        let direction = 0;
        let alive = true;
        return this;
    }

    method void draw() {
        do Screen.setColor(true);        
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    // Remove the player from the current position
    method void remove() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    // Set the direction of movement
    method void setDirection(int dir) {
        let direction = dir;
        return;
    }

    // Get x-coordinate
    method int getX() {
        return x;
    }
    // Get y-coordinate
    method int getY() {
        return y;
    }
    method int getWidth() {
        return width;
    }
    method int getHeight() {
        return height;
    }

    // Move the player in the current direction
    method void move() {                
        // Store previous position
        let prev_x = x;
        let prev_y = y;
        
        do remove();
        
        // Up
        if (direction = 1) {           
            if (y > (TwoPlayerGame.getTopBorder() + BOUNDARY_BUFFER)) {
                let y = y - 2;
            }
        }
        // Down
        if (direction = 2) {            
            if (y < (TwoPlayerGame.getBottomBorder() - height - BOUNDARY_BUFFER)) {
                let y = y + 2;
            }
        }
        // Left
        if (direction = 3) {
            if (x > (TwoPlayerGame.getLeftBorder() + BOUNDARY_BUFFER)) {
                let x = x - 2;
            }
        }
        // Right
        if (direction = 4) {
            if (x < (TwoPlayerGame.getRightBorder() - width - BOUNDARY_BUFFER)) {
                let x = x + 2;
            }
        }
        
        // Final boundary check
        // if (x < 0) { let x = 0; }
        // if (x > 501) { let x = 501; }
        // if (y < 0) { let y = 0; }
        // if (y > 244) { let y = 244; }
        
        do draw();
        return;
    }
    
    // Move back to previous position (for collision handling)
    method void moveBack() {
        // do remove();
        // let x = prev_x;
        // let y = prev_y;
        // do draw();
        return;
    }

    // Deallocate memory
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}
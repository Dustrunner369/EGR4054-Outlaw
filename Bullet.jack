class Bullet {
    field int x;
    field int y;
    field int direction; // 1 moves right, 2 moves left
    field int speed;
    field int width;
    field int height;
    
    constructor Bullet new(int x_in, int y_in, int dir) {        
        let x = x_in;
        let y = y_in;
        let width = 5;
        let height = 3;

        let direction = dir;
        let speed = 5; 
        return this;
    }
        
    // Draws bullet
    method void draw() {         
        // Make sure coords are in screen bounds    
        if ((x < (TwoPlayerGame.getRightBorder() - 2)) & (y < (TwoPlayerGame.getBottomBorder() - 2))) {                        
            do Screen.setColor(true);            
            do Screen.drawRectangle(x, y, x + width, y + height);                    
        }
        return;
    }

    // Removes bullet
    method void remove() {                
        // Make sure coords are in screen bounds
        if ((x < (TwoPlayerGame.getRightBorder() - 2)) & (y < (TwoPlayerGame.getBottomBorder() - 2))) {            
            do Screen.setColor(false);      
            do Screen.drawRectangle(x, y, x + width, y + height);                        
        }
        return;
    }
            
    method void move() {
        do remove();
                    
        // Move right
        if (direction = 1) { 
            let x = x + speed;            
        }
        // Move left
        if (direction = 2) {               
            let x = x - speed;                    
        }
        
        do draw();
        return;
    }
    
    // Accessors
    method int getX() {
        return x;
    }
    method int getY() {
        return y;
    }
    method int getDir() {
        return direction;
    }
    method int getWidth() {
        return width;
    }
    method int getHeight() {
        return height;
    }
    
    // Deallocate memory
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}